

import com.dsanti.buildsrc.Libs

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.mikepenz.aboutlibraries.plugin'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.dsanti.dcode"
        minSdk 24
        targetSdk 32
        versionCode 2
        versionName "0.15"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    buildFeatures {
        compose true
        buildConfig true
        viewBinding true

        // Disable unused AGP features
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    composeOptions {
        kotlinCompilerExtensionVersion Libs.AndroidX.Compose.version
    }


    packagingOptions {
        exclude "META-INF/licenses/**"
        exclude "META-INF/AL2.0"
        exclude "META-INF/LGPL2.1"
    }
}

dependencies {

    implementation platform(Libs.Firebase.bom)
    implementation Libs.Firebase.firebase_analytics_ktx
    implementation Libs.Firebase.fibase_crashlytics
    implementation Libs.Firebase.firebase_pref

    implementation Libs.aboutLibrariesCore

    implementation Libs.Kotlin.stdlib
    implementation Libs.Coroutines.android

    implementation Libs.AndroidX.dataStore


    implementation Libs.AndroidX.Activity.activityCompose
    implementation Libs.AndroidX.coreKtx
    implementation Libs.AndroidX.appcompat
    implementation Libs.AndroidX.Lifecycle.livedata
    implementation Libs.AndroidX.Lifecycle.viewModelCompose
    implementation Libs.AndroidX.Navigation.fragment
    implementation Libs.AndroidX.Navigation.uiKtx
    implementation Libs.AndroidX.Navigation.compose
    implementation Libs.material3
    implementation Libs.AndroidX.Room.room
    kapt Libs.AndroidX.Room.room_annotation

    implementation Libs.AndroidX.CameraX.camera_camera2
    implementation Libs.AndroidX.CameraX.camera_core
    implementation Libs.AndroidX.CameraX.camera_extensions
    implementation Libs.AndroidX.CameraX.camera_lifecycle
    implementation Libs.AndroidX.CameraX.camera_video
    implementation Libs.AndroidX.CameraX.camera_view


    implementation Libs.AndroidX.Compose.layout
    // TODO (M3): Remove this dependency when all components are available
    implementation Libs.AndroidX.Compose.material
    implementation Libs.AndroidX.Compose.Material3.material3
    implementation Libs.AndroidX.Compose.materialIconsExtended
    implementation Libs.AndroidX.Compose.toolingPreview
    implementation Libs.AndroidX.Compose.uiUtil
    implementation Libs.AndroidX.Compose.uiText
    implementation Libs.AndroidX.Compose.runtime
    implementation Libs.AndroidX.Compose.runtimeLivedata
    implementation Libs.AndroidX.Compose.viewBinding
    implementation Libs.AndroidX.Compose.accompanistUiController
    implementation Libs.AndroidX.Compose.accompanistPager
    implementation Libs.AndroidX.Compose.accompanistPermissions
    implementation Libs.AndroidX.Compose.accompanistNavigationAnimation
    implementation Libs.AndroidX.Compose.accompanistFlowLayout
    implementation Libs.AndroidX.Compose.accompanistWebView
    debugImplementation Libs.AndroidX.Compose.tooling

    debugImplementation Libs.AndroidX.Compose.uiTestManifest


    implementation Libs.QRCode.zxingAndroid
    implementation Libs.QRCode.zxingCore

    implementation Libs.UI.coil_kt
    implementation Libs.UI.fontAwesome
    implementation Libs.UI.about_libraries_ui
    implementation Libs.UI.crop_imager
    implementation Libs.UI.capturable


    

    androidTestImplementation Libs.junit
    androidTestImplementation Libs.AndroidX.Test.core
    androidTestImplementation Libs.AndroidX.Test.espressoCore
    androidTestImplementation Libs.AndroidX.Test.rules
    androidTestImplementation Libs.AndroidX.Test.Ext.junit
    androidTestImplementation Libs.AndroidX.Compose.uiTest

    // androidx.test is forcing JUnit, 4.12. This forces it to use 4.13
    configurations.configureEach {
        resolutionStrategy {
            force Libs.junit
        }
    }
}